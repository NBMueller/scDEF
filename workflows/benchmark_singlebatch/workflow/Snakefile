import os

configfile: "config/config.yaml"

DE_PROBS = config["de_probs"]
N_REPS = config["n_reps"]

rule all:
    input:
        fname = f'results/singlebatch_benchmark_figure.png'

rule generate_data:
    resources:
        mem_mb = 40000,
    params:
        de_prob = "{de_prob}",
        n_cells = config["n_cells"],
        n_genes = config["n_genes"],
        n_groups = config["n_groups"],
        seed = "{rep_id}",
    output:
        counts_fname = 'results/data/{de_prob}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_prob}/{rep_id}_meta.csv',
        plot_fname = 'results/data/{de_prob}/{rep_id}_plot.png',
    script:
        "scripts/generate_data.R"

rule run_scdef:
    resources:
        mem_mb = 40000,
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{de_prob}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_prob}/{rep_id}_meta.csv',
    output:
        ari_fname = 'results/scDEF/{de_prob}/{rep_id}_ari.txt',
        mod_fname = 'results/scDEF/{de_prob}/{rep_id}_mod.txt',
        ent_fname = 'results/scDEF/{de_prob}/{rep_id}_ent.txt',
    script:
        "scripts/run_scdef.py"

rule run_schpf:
    resources:
        mem_mb = 40000,
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{de_prob}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_prob}/{rep_id}_meta.csv',
    output:
        ari_fname = 'results/scHPF/{de_prob}/{rep_id}_ari.txt',
        mod_fname = 'results/scHPF/{de_prob}/{rep_id}_mod.txt',
        ent_fname = 'results/scHPF/{de_prob}/{rep_id}_ent.txt',
    script:
        "scripts/run_schpf.py"

rule run_scvi:
    resources:
        mem_mb = 40000,
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{de_prob}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_prob}/{rep_id}_meta.csv',
    output:
        ari_fname = 'results/scVI/{de_prob}/{rep_id}_ari.txt',
        mod_fname = 'results/scVI/{de_prob}/{rep_id}_mod.txt',
        ent_fname = 'results/scVI/{de_prob}/{rep_id}_ent.txt',
    script:
        "scripts/run_scvi.py"

rule gather_scores:
    input:
        fname_list = expand(
            'results/{method}/{de_prob}/{rep_id}_ent.txt',
            method=["scDEF", "scHPF", "scVI"], de_prob=DE_PROBS,
            rep_id=[r for r in range(N_REPS)])
    output:
        'results/scores.csv'
    script:
        'scripts/gather_scores.py'

rule plot_benchmark:
    input:
        'results/scores.csv'
    output:
        'results/singlebatch_benchmark_figure.png',
    script:
        "scripts/plot_benchmark.py"
