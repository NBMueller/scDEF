import os

configfile: "config/config.yaml"

DE_FACSCALES = config["de_facscale"]
N_REPS = config["n_reps"]

rule all:
    input:
        fname = f'results/singlebatch_benchmark_figure.png'

rule generate_data:
    params:
        de_prob = config["de_prob"],
        de_facscale = "{de_facscale}",
        n_cells = config["n_cells"],
        n_genes = config["n_genes"],
        n_groups = config["n_groups"],
        seed = "{rep_id}",
    output:
        counts_fname = 'results/data/{de_facscale}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_facscale}/{rep_id}_meta.csv',
        plot_fname = 'results/data/{de_facscale}/{rep_id}_plot.png',
    script:
        "scripts/generate_data.R"

rule run_scdef:
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{de_facscale}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_facscale}/{rep_id}_meta.csv',
    output:
        fname = 'results/scDEF/{de_facscale}/{rep_id}_ari.txt',
    script:
        "scripts/run_scdef.py"

rule run_schpf:
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{de_facscale}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_facscale}/{rep_id}_meta.csv',
    output:
        fname = 'results/scHPF/{de_facscale}/{rep_id}_ari.txt',
    script:
        "scripts/run_schpf.py"

rule run_scvi:
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{de_facscale}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{de_facscale}/{rep_id}_meta.csv',
    output:
        fname = 'results/scVI/{de_facscale}/{rep_id}_ari.txt',
    script:
        "scripts/run_scvi.py"

rule gather_scores:
    input:
        fname_list = expand(
            'results/{method}/{de_facscale}/{rep_id}_ari.txt',
            method=["scDEF", "scHPF", "scVI"], de_facscale=DE_FACSCALES,
            rep_id=[r for r in range(N_REPS)])
    output:
        'results/scores.csv'
    script:
        'scripts/gather_scores.py'

rule plot_benchmark:
    input:
        'results/scores.csv'
    output:
        'results/singlebatch_benchmark_figure.png',
    script:
        "scripts/plot_benchmark.py"
