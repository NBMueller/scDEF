import os

configfile: "config/config.yaml"

N_REPS = config["n_reps"]
N_BATCHES = config["n_batches"]
FRACS_SHARED = config["frac_shared"]

rule all:
    input:
        fname = f'results/multibatch_benchmark_figure.png'

rule generate_data:
    params:
        de_prob = config["de_prob"],
        batch_facscale = config["batch_facscale"],
        n_cells = config["n_cells"],
        n_genes = config["n_genes"],
        n_groups = config["n_groups"],
        n_batches = "{n_batches}",
        frac_shared = "{frac_shared}",
        seed = "{rep_id}",
    output:
        counts_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_meta.csv',
        plot_groups_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_plot_groups.png',
        plot_batches_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_plot_batches.png',
    script:
        "scripts/generate_data.R"

rule run_scdef:
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_meta.csv',
    output:
        ari_fname = 'results/scDEF/{n_batches}/{frac_shared}/{rep_id}_ari.txt',
        mod_fname = 'results/scDEF/{n_batches}/{frac_shared}/{rep_id}_mod.txt',
    script:
        "scripts/run_scdef.py"

rule run_schpf:
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_meta.csv',
    output:
        ari_fname = 'results/scHPF/{n_batches}/{frac_shared}/{rep_id}_ari.txt',
        mod_fname = 'results/scHPF/{n_batches}/{frac_shared}/{rep_id}_mod.txt',
    script:
        "scripts/run_schpf.py"

rule run_scvi:
    params:
        seed = "{rep_id}",
    input:
        counts_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_counts.csv',
        meta_fname = 'results/data/{n_batches}/{frac_shared}/{rep_id}_meta.csv',
    output:
        ari_fname = 'results/scVI/{n_batches}/{frac_shared}/{rep_id}_ari.txt',
        mod_fname = 'results/scVI/{n_batches}/{frac_shared}/{rep_id}_mod.txt',
    script:
        "scripts/run_scvi.py"

rule gather_scores:
    input:
        fname_list = expand(
            'results/{method}/{n_batches}/{frac_shared}/{rep_id}_mod.txt',
            method=["scDEF", "scHPF", "scVI"], frac_shared=FRACS_SHARED,
            n_batches=N_BATCHES, rep_id=[r for r in range(N_REPS)])
    output:
        'results/scores.csv'
    script:
        'scripts/gather_scores.py'

rule plot_benchmark:
    input:
        'results/scores.csv'
    output:
        'results/multibatch_benchmark_figure.png',
    script:
        "scripts/plot_benchmark.py"
